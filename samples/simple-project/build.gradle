repositories { mavenCentral() }

apply plugin: 'vertx'

vertx {
  platform {
    java '2.0.1-final'
    tools '2.0.1-final'
  }

  module {
    group 'com.darylteo'
    name 'simple-project'
    version '0.1.0-SNAPSHOT'
  }

  deployments {
    mod {
      config{ foo 'bar' }

      deploy('org.crashub~vertx.shell~2.0.2') { 
        hello 'world' 
      }
    }
  }

  info {
    developers {
      developer {
        id 'darylteo'
        name 'Daryl Teo'
        email 'i.am@darylteo.com'
      }
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
  }
}

// Tests
afterEvaluate {
  // test deployments and tasks
  assert vertx.deployments.findByName('mod') != null
  assert tasks.findByName('runMod') != null
  assert tasks.findByName('debugMod') != null

  // test deployment configurations
  assert vertx.deployments.mod.config.foo != null
  assert vertx.deployments.mod.config.foo == 'bar'

  assert vertx.deployments.mod.modules.size() == 2
  assert vertx.deployments.mod.modules[1].config.foo == null
  assert vertx.deployments.mod.modules[1].config.hello == 'world'
  
  assert vertx.deployments.mod.modules[1].effectiveConfig.foo == 'bar'
  assert vertx.deployments.mod.modules[1].effectiveConfig.hello == 'world'
}