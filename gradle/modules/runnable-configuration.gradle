import org.vertx.java.core.Handler
import org.vertx.java.platform.PlatformLocator
import org.vertx.java.platform.impl.ModuleClassLoader

// This sets up the classpath for the script itself
// This is required for the class imports above
buildscript {
  repositories {
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    mavenCentral()
  }

  dependencies {
    classpath "io.vertx:vertx-core:$vertxVersion"
    classpath "io.vertx:vertx-platform:$vertxVersion"
  }
}

defaultTasks = ['assemble']

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

if (produceJar == 'false') {
  info "Jars have been disabled for this module."
  jar.enabled = false
  assert configurations.archives.artifacts.removeAll { it.file == jar.archivePath }
}

configurations {
  provided
  testCompile.extendsFrom provided
}

repositories {
  mavenLocal()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  mavenCentral()
}

dependencies {
  provided "io.vertx:vertx-core:$vertxVersion"
  provided "io.vertx:vertx-platform:$vertxVersion"
  testCompile "junit:junit:$junitVersion"
  testCompile "io.vertx:testtools:$toolsVersion"
}

sourceSets {
  main {
    compileClasspath = compileClasspath + configurations.provided
  }
}

info "Adding Tasks"
task copyMod(type:Copy, dependsOn: 'classes', description: 'Assemble the module into the local mods directory' ) {
  // Copy into module directory
  into "$rootDir/mods/$project.moduleName"
  from compileJava
  from 'src/main/resources'

  // and then into module library directory
  into( 'lib' ) {
    from configurations.compile
  }
}

task modZip( type: Zip, dependsOn: 'pullInDeps', description: 'Package the module .zip file') {
  group = 'vert.x'
  description = "Assembles a vert.x module"
  destinationDir = project.file('build/libs')
  archiveName = "${project.artifact}-${project.version}" + ".zip"
  from copyMod
}

task pullInDeps(dependsOn: copyMod, description: 'Pull in all the module dependencies for the module into the nested mods directory') << {
  if (pullInDeps == 'true') {
    def pm = PlatformLocator.factory.createPlatformManager()
    System.out.println("Pulling in dependencies for module $project.moduleName Please wait")
    pm.pullInDependencies(project.moduleName)
    System.out.println("Dependencies pulled into mods directory of module")
  }
}

task("run-$project.artifact", dependsOn: copyMod, description: 'Run the module using all the build dependencies (not using installed vertx)') << {
  def mutex = new Object()

  ModuleClassLoader.reverseLoadOrder = false
  def pm = PlatformLocator.factory.createPlatformManager()
  pm.deployModule(project.moduleName, null, 1, new Handler<String>() {
    public void handle(String deploymentID) {
      if (!deploymentID){
        error 'Verticle failed to deploy.'

        // Wake the main thread
        synchronized(mutex){
          mutex.notify()
        }
        return
      }

      info "Verticle deployed! Deployment ID is: $deploymentID"
      info 'CTRL-C to stop server'
    }
  });

  // Waiting thread so that Verticle will continue running
  synchronized (mutex){
    mutex.wait()
  }
}

info "Performing common configuration"
artifacts {
  archives modZip
}

task cleanModsDir(type: Delete) {
  delete 'mods'
}

clean {
  dependsOn cleanModsDir
}

test {
  dependsOn copyMod

  // Make sure tests are always run!
  outputs.upToDateWhen { false }

  // Show output
  testLogging.showStandardStreams = true

  testLogging { exceptionFormat "full" }
}