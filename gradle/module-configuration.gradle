info "Loading vertx.properties and vertx.gradle"

loadProperties()
loadBuildfile()

info "Module Name: $verticle.moduleName"

if (verticle.runnable) {
  project.apply from: "$rootDir/gradle/runnable-configuration.gradle"
} else {
  project.apply from: "$rootDir/gradle/nonrunnable-configuration.gradle"
}

def loadBuildfile() {
  project.apply from: "$projectDir/$VERTX_GRADLE_FILENAME"
}

def loadProperties() {
  def config = new Properties()
  def propFile = new File("$projectDir/$VERTX_PROPERTIES_FILENAME")

  if(!propFile.canRead()){
    return
  }

  extensions.create('verticle', ModuleProps)

  config.load(new FileInputStream(propFile))
  for (Map.Entry property in config) {
    verticle[property.key] = property.value;
  }
}

class ModuleProps {
  def repotype="maven"
  def groupname="my-company"
  def artifact="my-module"
  def version="1.0.0-SNAPSHOT"

  def testtimeout=300
  def runnable=true

  def getModuleName() {
    return "$repotype:$groupname:$artifact:$version"
  }
}