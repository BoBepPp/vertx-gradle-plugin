apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenLocal()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  mavenCentral()
}

if (!project.hasProperty('release')){
  ext.release = false;
}

if(!release){
  version = "$version-SNAPSHOT"
}

dependencies {
  compile gradleApi()
  groovy localGroovy()

  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-platform:$vertxVersion"
  compile "io.vertx:testtools:$toolsVersion"

  testRuntime "$group:$artifact:$version"
}

test {
  testLogging.showStandardStreams = true
}

// These tasks are required for maven/sonatype
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from "$buildDir/docs/javadoc"
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { release && gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

install {
  repositories.mavenInstaller {
    pom.artifactId = artifact
  }
}

uploadArchives {
  group 'build'
  description = "Does a maven deploy of archives artifacts"

  repositories {
    mavenDeployer {
      setUniqueVersion(false)

      configuration = configurations.archives

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      if (release) {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      }

      pom {
        artifactId = artifact
        project {
          name 'Vertx Gradle Plugin'
          description 'Gradle Plugin for Vert.x Projects'
          inceptionYear '2013'
          url 'http://github.com/darylteo/vertx-gradle-template'

          developers {
            developer {
              id 'darylteo'
              name 'Daryl Teo'
              email 'i.am@darylteo.com'
            }
          }

          scm {
            url 'http://github.com/darylteo/vertx-gradle-template'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }
          properties {
            setProperty('project.build.sourceEncoding', 'UTF8')
          }
        }
      }
    }
  }
}